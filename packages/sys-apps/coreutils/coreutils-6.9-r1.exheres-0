# Copyright 2007-2008 Bryan Ã˜stergaard
# Distributed under the terms of the GNU General Public License v2

require autotools

DESCRIPTION="Core file, shell and text utilities expected to be on any system."
HOMEPAGE="http://www.gnu.org/software/coreutils"
SRC_URI="http://ftp.gnu.org/gnu/${PN}/${P}.tar.gz"

LICENSE="GPL-2"
SLOT="0"
PLATFORMS="~amd64 ~ia64 ~x86"
MYOPTIONS="nls acl"

DEPENDENCIES="build:
                acl? ( sys-apps/acl sys-apps/attr )"

src_prepare() {
    expatch -p1 "${FILESDIR}/${P}-futimens.patch" \
        "${FILESDIR}/009_all_coreutils-tests.patch"
    AT_M4DIR="m4" eautoreconf || die "Eautoreconf failed."
}

DEFAULT_SRC_CONFIGURE_OPTION_ENABLES="acl nls"

src_install() {
    emake DESTDIR="${D}" install || die "Install failed."

    local binfiles="basename cat chgrp chmod chown cp chroot \
                    cut date df dir dirname du echo env expr \
                    false head ln ls mkdir mkfifo mknod mv pwd \
                    readlink rm rmdir seq sleep sort stty sync \
                    tail touch tr true tty uname vdir wc yes"

    dodir /bin
    for binfile in ${binfiles}; do
        mv "${D}"/usr/bin/${binfile} "${D}"/bin/${binfile}
        dosym /bin/${binfile} /usr/bin/${binfile} || die "Dosym failed."
    done

    insinto /etc
    newins src/dircolors.hin DIR_COLORS || die "newins failed."
}

src_test() {
    addwrite /dev/full
    export RUN_EXPENSIVE_TESTS="yes"
    make check || die "Make check failed"
}
