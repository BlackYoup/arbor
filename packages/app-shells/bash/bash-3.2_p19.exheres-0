# Copyright 2007 Bryan Ã˜stergaard
# Distributed under the terms of the GNU General Public License v2

PATCH_LEVEL=${P##*_p}

MY_P=${P%_p*}
S="${WORKDIR}/${MY_P}"

DESCRIPTION="Bash shell"
HOMEPAGE="http://www.gnu.org/software/bash"
SRC_URI="mirror://gnu/bash/${MY_P}.tar.gz
        $(for ((i=1; i<=PATCH_LEVEL; i++)); do
            printf "mirror://gnu/bash/${MY_P}-patches/bash32-%03d\n" ${i}
        done)"

LICENSE="GPL-2"
SLOT="0"
PLATFORMS="~amd64 ~ia64 ~x86"
MYOPTIONS="nls"

DEPENDENCIES="build,run:
                sys-libs/ncurses"

src_unpack() {
    unpack "${MY_P}.tar.gz"
    cd "${S}"

    local i
    for ((i=1; i<=PATCH_LEVEL; i++)); do
        patch -p0 -s -f < "${FETCHEDDIR}/bash32-"$(printf "%03d" ${i}) || die "patching failed."
    done

    patch -p1 -s -f < "${FILESDIR}/${MY_P}-config.diff"
}

src_compile() {
    econf \
        --bindir=/bin \
        --without-installed-readline \
        --with-curses \
        $(option_enable nls) \
        || die "configure failed."
    emake || die "make failed."
}

src_install() {
    emake DESTDIR="${D}" install || die "install failed."
    cd "${D}/bin"
    ln -s /bin/bash sh

    insinto /etc/bash
    doins "${FILESDIR}/bashrc"

    keepdir /etc/bash/bashrc.d
}
